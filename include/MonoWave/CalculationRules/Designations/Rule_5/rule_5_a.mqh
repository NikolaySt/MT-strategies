
int Rule_5_a(){
////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Условие “а”: дължината на m0 e по-малка от 100% от дължината на m1
////////////////////////////////////////////////////////////////////////////////////////////////////////////  
   if (WavesCountInDirection(2) > 3){
   //**(ако m2 се състои от повече от три моновълни [или група моновълни] )
      InfoParagraph = "5-a-i:1";
      Rule_5_a_i_paragraph_1();
      InfoParagraph = "5-a-i:2";
      Rule_5_a_i_paragraph_2();
   }else{
   //**(ако m2 се състои от не повече от три моновълни [или група моновълни] )
      InfoParagraph = "5-a-ii:1";
      Rule_5_a_ii_paragraph_1();
      InfoParagraph = "5-a-ii:2";
      Rule_5_a_ii_paragraph_2();
      InfoParagraph = "5-a-ii:3";
      Rule_5_a_ii_paragraph_3();
      InfoParagraph = "5-a-ii:4";
      Rule_5_a_ii_paragraph_4();
      InfoParagraph = "5-a-ii:5";
      Rule_5_a_ii_paragraph_5();
      InfoParagraph = "5-a-ii:6";
      Rule_5_a_ii_paragraph_6();
      InfoParagraph = "5-a-ii:7";
      Rule_5_a_ii_paragraph_7();
      InfoParagraph = "5-a-ii:8";
      Rule_5_a_ii_paragraph_8();
      InfoParagraph = "5-a-ii:9";
      Rule_5_a_ii_paragraph_9();
      InfoParagraph = "5-a-ii:10";
      Rule_5_a_ii_paragraph_10();
      InfoParagraph = "5-a-ii:11";
      Rule_5_a_ii_paragraph_11();
      InfoParagraph = "5-a-ii:12";
      Rule_5_a_ii_paragraph_12();
      InfoParagraph = "5-a-ii:13";
      Rule_5_a_ii_paragraph_13();
   }      
}

//----------------------------------------------------------------------------------------------------------
//**(ако m2 се състои от повече от три моновълни [или група моновълни] )
//----------------------------------------------------------------------------------------------------------

int Rule_5_a_i_paragraph_1(){
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако дължината на първите три моновълни на m2 е не повече от 61.8% от дължината на m1, 
то може да започава Сложна Корекция от първата или втората от тези моновълни (след m1), 
направление противоположно на m1 и представляваща х-вълна, 
ИЛИ m1 съдържа в центъра пропаднала х-вълна  или пропаднала b-вълна , 
ИЛИ m1 представлява трета вълна на Импуслна Ценова Фигура (Трендови или Терминален импулс) с неуспешна пета; 
в края на m1 поставете обозначение “:5/:s5”. 
Ако дължината на първите три моновълни след m1 е минимум 25% от дължината на m1, 
добавете към списъка следното обозначение “:F3”. 
ЗАБЕЛЕЖКА: разглеждайки варината за пропаднала х-вълна, 
поставете кръгче в центъра на m1 и до него в лявао “:5?”, а отдясно напишете “х:с3?”. 
Обмисляйки варианта с пропаднала b-вълна, 
поставете кръгче в центъра на m1 и до него в ляво напишете “5?” а отдясно “b:F3?”. 
В последният случай факта на завършена Сложна Корекция ще бъде подтвърден веднага преди рязкото обръщане на пазара 
(в направление противоположно на m1), и надвишява нивото от 61.8% на m1.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/ 
   
   double ratio_p = CompareLengthWithCountWaves(1, 2, /*countwave*/3);
   if (Small_061(ratio_p)){
      AddDesignation(desig_5, "");
      AddDesignation(desig_s5, "");
      if (Large_025(ratio_p)){
         AddDesignation(desig_F3, "");   
      }
      AddDesignation(desig_fall, "1");
   }
      
   return(0);
}

int Rule_5_a_i_paragraph_2(){
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако дължината на първите три моновълни от m2 превишава 61.8% дължината на m1, 
то може да завършва или а-вълна на Плоска Корекция със сложна b-вълна, 
или трета вълна на Импулсна ценова фигура с неуспешна 5-та.; 
в края на m1 поставете обозначение “:F3/:5” описващо тези два алтернативни варианта.
////////////////////////////////////////////////////////////////////////////////////////////////////////////   
*/ 
   double ratio_p = CompareLengthWithCountWaves(1, 2, /*countwave*/3);
   if (Large_061(ratio_p)){
      AddDesignation(desig_F3, "");
      AddDesignation(desig_5, "");
   }
   
   return(0);
}


//----------------------------------------------------------------------------------------------------------
//**(ако m2 се състои от не повече от три моновълни [или група моновълни] )
//----------------------------------------------------------------------------------------------------------

int Rule_5_a_ii_paragraph_1(){
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 (плюс една единица време) се достига за време, 
не превишаващо продължителността на нейното формиране, 
и ценовите проекции на m(-2) и m0 не се застъпват дори и частично, и m2 е по-дълга от m(-2), 
и дължината и/или продължителността на m(-2) и m0 значително се отличават една от друга, 
и m(-1) е по-дълга от m1 и / или от m(-3), то m1 може да завършва ценова фигура Трендови Импулс; 
поставете в края на m1 обозначение “:L5”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/            
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break <= (time_1+1) && time_1_break > 0){  
      
      if (!OverlapDirection(-2, 0)){
      
         double ratio_p = ComparePriceDirection(2, -2);      
         if (Large_100(ratio_p)){         
         
            ratio_p = ComparePriceDirectionFull(-2, 0);
            double ratio_t = CompareTimeDirectionFull(-2, 0);
            if (!Equal_100(ratio_p) || !Equal_100(ratio_t)){
               
               ratio_p = ComparePriceDirection(-1, 1);   
               double ratio_p_1 = ComparePriceDirection(-1, -3);   
               if (Large_100(ratio_p) || Large_100(ratio_p_1)){
                  
                  AddDesignation(desig_L5, "");    
               }              
            }
         }
      }
   }    

   return(0);   
} 

int Rule_5_a_ii_paragraph_2(){
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 (плюс една единица време) се достига за време, 
не превишаващо продължителността на нейното формиране,и m2 е по-дълга от m(-2), и m(-4) е по-дълга от m(-3), 
то m1 може да е край на ЗигЗаг или Плоска Корекция; поставете в края на m1 обозначние “:L5”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/            
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1); 
   
   if (time_1_break <= (time_1+1) && time_1_break > 0){  
      
      double ratio_p = ComparePriceDirection(2, -2);       
      if (Large_100(ratio_p)){
         
         ratio_p = ComparePriceDirection(-4, -3); 
         if (Large_100(ratio_p)){
            AddDesignation(desig_L5, ""); 
         }
         
      } 
   }
   return(0);   
} 

int Rule_5_a_ii_paragraph_3(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 (плюс една единица време) се достига за време, 
не превишаващо продължителността на нейното формиране, m2 е по-дълга от m(-2), и m(-3) е по-дълга от m(-2) и от m(-4), 
то в m1  може да завъшва Стандартна Ценова фигура на Елиът,явяваща се част от Сложна Корекция, където m(-2) да бъде х-вълна; 
поставете в края на m1 обозначение “:L5”,а в края на m(-2) обозначение “х:с3?”. 
Ако в тези описания установите че дължината на m(-1) е минимум 161.8% от дължината на m0 то се образува Стадартна Корекция, 
по всяка вероятност ще бъде ЗигЗаг. Ако дължината на m(-1) е не по-малка от 100%, но по-малка от 161.8% от дължината на m0, 
Стадартната Корекция ще е Плоска.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break <= (time_1+1) && time_1_break > 0){  
      
      double ratio_p = ComparePriceDirection(2, -2);       
      if (Large_100(ratio_p)){
      
         ratio_p = ComparePriceDirection(-3, -2);       
         if (Large_100(ratio_p)){

            ratio_p = ComparePriceDirection(-3, -4);       
            if (Large_100(ratio_p)){
            
               AddDesignation(desig_L5, "");
               AddDesignation(desig_xc3_, "-2");                                        
            }                   
         }          
      }      
   }
   return(0);    
} 
 

int Rule_5_a_ii_paragraph_4(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 (плюс една единица време) се достига за време, 
не превишаващо продължителността на нейното формиране, и m2 е по-кратка от m(-2), 
то m1 може да бъде край на Плоска Кореция или ЗигЗаг; поставете в края на m1 обозначение “:L5”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break <= (time_1+1) && time_1_break > 0){  
      
      double ratio_p = ComparePriceDirection(2, -2);       
      if (Small_100(ratio_p)){

         AddDesignation(desig_L5, "");          
      } 
   }
   return(0);    
} 
 
int Rule_5_a_ii_paragraph_5(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 (плюс една единица време) се достига за време непревишаващо времето за нейното формиране, 
и m(-2) е по-кратка от m(-1), и m(-1) не е най-късата от m(-3) и m1,и дължината на m3 не превишава 61.8% дължината на m2, 
и началното ниво на m(-3) се достига (или пресича) за време не повече от 50% от период от началото на m(-3) до края на m1, 
и крайното ниво на m1 не се пресича за четери периода време,  от нейното формиране, 
и ценовата проекция на група вълни m2-m4 и минимум двапъти превишава аналогичния параметър m1, 
и “:с3” се явява едино от възможните Сктрукторни обозначения на вълна m(-1), 
то m1 може да завършва Терминален импулс; добавете към списъка на m1 обозначение “:L3”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break <= (time_1+1) && time_1_break > 0){  
      
      double ratio_p = ComparePriceDirection(-2, -1);       
      if (Small_100(ratio_p)){
      
         if (NoSmallerDirection(-1, -3, 1)){
         
            ratio_p = ComparePriceDirection(3, 2);       
            if (Small_061(ratio_p)){ 
               
               if (CheckTimeGoToBeginning(-3, DirectionSumTimeLength(-3, 1)*0.6,  1)){
                  
                  
                  if (!CheckBreakLevelInTime(1, time_1*4, 1)){
                     
                     ratio_p = ComparePrice(DirectionSumPriceLength(2, 4), DirectionPriceLenght(1)); 
                     
                     if (Large_161(ratio_p)){
                        AddDesignation(desig_L3, "");    
                     }
                  }                                      
               }
            }
         }
      } 
   }
   return(0);    
}  

int Rule_5_a_ii_paragraph_6(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 (плюс една единица време) се достига за време не превишаващо времето за нейното формиране, 
и дължината на m3 се намира в границите 61.8% - 100% от дължината на m1, 
то m1 може да бъде част от Плоска Неправилна Неуспешна, ето защо добавете в края обозначение “:F3”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break <= (time_1+1) && time_1_break > 0){  
   
      double ratio_p = ComparePriceDirection(3, 1);       
      if (Large_061(ratio_p) && Small_100(ratio_p)){      
      
         AddDesignation(desig_F3, "");    
      }
   }
   return(0);    
}  

int Rule_5_a_ii_paragraph_7(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 (плюс една единица време) се достига за време не превишаващо времето за нейното формиране, 
и m3 е по-дълга от m2, и дължината на m0 е минимум 161.8% от дължината на m2, 
и началното ниво на m3 се достига за време, не превишаващо продължителността на m3, 
то m1 може да бъде част от  Неправилна Плоска; поставете в края на m1 обозначение “:F3”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break <= (time_1+1) && time_1_break > 0){  
   
      double ratio_p = ComparePriceDirection(3, 2);       
      if (Large_100(ratio_p)){      
         
         ratio_p = ComparePriceDirection(0, 2);       
         
         if (Large_161(ratio_p)){ 
            
            double time_3_break = TimeBreakDirection(3);
            double time_3 = TimeDirection(3);   
            if (time_3_break <= time_3 && time_3_break > 0){             
            
               AddDesignation(desig_F3, ""); 
                  
            }
            
         }
            
      }
   }
   return(0);    
}

int Rule_5_a_ii_paragraph_8(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 се достига за време, по-голямо от продължителността на m1, 
и дължината на m2 не превишава 61.8% от ценовото разстояние определно от началото на m(-1) до края на m1, 
и m3 е по-кратка от m2, то е възможно да завършва Сложна Корекция в която m1 ще бъде най-високата/ниската 
точка достигната от пазара за период най малко 2-пъти комбинираното време на m0-m2, 
поставете в края на m1 обозначение “:F3”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break > time_1 && time_1_break > 0){  
      
      double ratio_p = ComparePrice(DirectionPriceLenght(2), DirectionSumPriceLength(-1, 1));        
      if (Small_061(ratio_p)){      
                     
         ratio_p = ComparePriceDirection(3, 2);       
         if (Small_100(ratio_p)){
            AddDesignation(desig_F3, "");
         } 
            
      }
   }
   return(0);    
}

int Rule_5_a_ii_paragraph_9(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 се дотига за време по-голямо от продължителността на m1, m3 е по-дълга от m2, 
и дължината  на m2 не превишава 61.8% от ценовото разстояние определено от началото на m(-1) до края на m1, 
то пазара може да образува Сложна Корекция (края на една от корективните фази може да се яви m1, 
а m2 да бъде х-вълна) или Разширяващ Триъгълник; поставете в края на m1 обозначенията “:F3/:c3/:L5”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break > time_1 && time_1_break > 0){  
      
      double ratio_p = ComparePrice(DirectionPriceLenght(2), DirectionSumPriceLength(-1, 1));        
      if (Small_061(ratio_p)){      
                     
         ratio_p = ComparePriceDirection(3, 2);       
         if (Large_100(ratio_p)){
            AddDesignation(desig_F3, "");
            AddDesignation(desig_c3, "");
            AddDesignation(desig_L5, "");
         } 
            
      }
   }
   return(0);    
}

int Rule_5_a_ii_paragraph_10(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 се достига за време, по-голямо от продължителността на m1, и m2 е по-кратка от m(-2), 
то m1 може да завършава ЗигЗаг, влизащ в състава на Сходящ Триъгълник; поставете в края на m1 обозначение “:L5”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break > time_1 && time_1_break > 0){          
                     
         double ratio_p = ComparePriceDirection(2, -2);       
         if (Small_100(ratio_p)){
            AddDesignation(desig_L5, "");
         } 
            

   }
   return(0);    
}

int Rule_5_a_ii_paragraph_11(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако началното ниво на m1 се достига за време, по-голямо от продължителността на m1, 
и дължината на m(-1) е минимум 61.8% от дължината на m1, и m3 е по-кратка от m2, 
и началното ниво на m3 (плюс една единица време) се достига за време не превишаващо продължителността на m3, 
то m1 може да бъде част от Плоска корекция, завъшваща по голяма ценова фигура, 
затова поставете в края на m1 обозначение “:F3”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   double time_1_break = TimeBreakDirection(1);
   double time_1 = TimeDirection(1);   
   if (time_1_break > time_1 && time_1_break > 0){          
                     
         double ratio_p = ComparePriceDirection(-1, 1);       
         if (Large_061(ratio_p)){
         
            ratio_p = ComparePriceDirection(3, 2);       
            if (Small_100(ratio_p)){

               double time_3_break = TimeBreakDirection(3);
               double time_3 = TimeDirection(3);   
               if (time_3_break <= (time_3+1) && time_3_break > 0){             
            
                  AddDesignation(desig_F3, ""); 
                  
               }            
            
            }
         } 
            

   }
   return(0);    
}


int Rule_5_a_ii_paragraph_12(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако m3 е по-дълга от m2, и m4 е по-дълга от m3, и дължината на m0 е по-малка от 61.8% от дължината на m1, 
то вълна m1 може да е начало на Разходящ Триъгълник; поставете в края на m1 обозначение “(:F3)”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
        
                     
   double ratio_p = ComparePriceDirection(3, 2);       
   if (Large_100(ratio_p)){
   
      ratio_p = ComparePriceDirection(4, 3);       
      if (Large_100(ratio_p)){
      
         ratio_p = ComparePriceDirection(0, 1);       
         if (Small_061(ratio_p)){                 
      
            AddDesignation(desig_F3, "()"); 
            
         }                  
      }
   } 

   return(0);    
}


int Rule_5_a_ii_paragraph_13(){     
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако m3 е по-дълга от m2, и m4 е по-дълга от m3,  и дъжината на m0 е в границите 61.8% - 100% от дължината на m1, 
то е възможно да се формира Разходящ Триъгълник; поставете в края на m1 обозначение “(:с3)”.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
        
                     
   double ratio_p = ComparePriceDirection(3, 2);       
   if (Large_100(ratio_p)){
   
      ratio_p = ComparePriceDirection(4, 3);       
      if (Large_100(ratio_p)){
      
         ratio_p = ComparePriceDirection(0, 1);       
         if (Large_061(ratio_p) && Small_100(ratio_p)){                 
      
            AddDesignation(desig_c3, "()"); 
            
         }                  
      }
   } 

   return(0);    
}