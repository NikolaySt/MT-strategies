#include <MonoWave\CalculationRules\Designations\Rule_1\Utils_Rule_1.mqh>

bool equal_time_price_m0_m2;
int Rule_1_a(){
////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Условие “а”: дължината на m0 по-малка от 61.8% от дължината m1
////////////////////////////////////////////////////////////////////////////////////////////////////////////   
   equal_time_price_m0_m2 = Equal_price_time_100_61_m0_m2();      
   
   InfoParagraph = "1-а:1";  
   Rule_1_a_paragraph_1();  
   
   InfoParagraph = "1-а:2";  
   Rule_1_a_paragraph_2();
   
   InfoParagraph = "1-а:3";
   Rule_1_a_paragraph_3();
   
   InfoParagraph = "1-а:4";
   Rule_1_a_paragraph_4();
   
   InfoParagraph = "1-а:5";
   Rule_1_a_paragraph_5();
   
   InfoParagraph = "1-а:6";
   Rule_1_a_paragraph_6();
}


int Rule_1_a_paragraph_1(){   
/* 
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако продължителността на m2 е по-голяма или равна от продължителността на m1 ИЛИ от продулжителността на m3, 
поставете в края на m1 означение “:5”. Ако дължината на m(-1) се намира в границите 100-161,8% (включително) 
от дължината на m0, и дължината на m0 е много близко до 61,8% от дължината на m1, и m4 не пресича края на m0, 
то m1 може да завършва Плоска ценова фигура от Сложна конфигурация, където m2 – х вълна “(x:c3)”; 
поставете в края на m1 обозначение “:s5”.  
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/                      
   int time_m1 = TimeDirection(1);      
   int time_m2 = TimeDirection(2);    
   int time_m3 = TimeDirection(3);   
   
   if (time_m2 >= time_m1 || time_m2 >= time_m3){      
      AddDesignation(desig_5, "");  
      
      double ratio_p = ComparePriceDirection(-1, 0);      
      if (Large_100(ratio_p) && Small_161(ratio_p)){   
      
         ratio_p = ComparePriceDirection(0, 1);
         if (Equal_061(ratio_p)){
                  
            if (CheckDirection(0) == -1){
               if (DirectionMinLevel(4) > DirectionEndPrice(0)){
                  AddDesignation(desig_s5, "");
                  AddDesignation(desig_xc3_, "2");
               } 
            }else{
               if (DirectionMaxLevel(4) < DirectionEndPrice(0)){
                  AddDesignation(desig_s5, "");
                  AddDesignation(desig_xc3_, "2");
               }        
            }    
         }              
      }
   }
   return(0);
} 
int Rule_1_a_paragraph_2(){
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако m0 се състои повече от три моновълни и m1 достига началото равнище на m0 
за време не превишаващо от времемто за формиране на m0, 
следователно с m0 може да завършва важна ценова фигура на Елият, отбележете това на графиката.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/ 
   int time_m0_break = TimeBreakDirection(0);
   int time_m0 = TimeDirection(0);
   
   int count_wave = WavesCountInDirection(0);
   if (count_wave > 3 && time_m0_break <= time_m0 && time_m0_break > 0){
      AddDesignation(desig_note, "0");         
   }
   return(0);
}

int Rule_1_a_paragraph_3(){
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////                             
Ако дължината и продължителността на m0 и m2 са равни (или се отнасят с коефициент 61.8%) и 
дължината на m(-1) не е по-малка от 161.8% от дължината на m1, и m3 (или група вълни от m3 до m5) 
достигат дължината на m(-1) за време не превишаващо времето за формиране на m(-1), 
по всяка вероятност се формира разновидност на подвижна корекция. 
Отбележете това на графиката и прибавете обозначение “[:c3]” към обозначение “:5” в края на m1. 
Ако се образува проста разновидност на подвижна корекция то началото ще е от началото на m0, 
и ще завърши в края на m2, а m1 ще се бъде b-вълна в тази корекция. 
Когато подвижната корекция е сложна двойна тройка, m(-2) трябва да бъде кратка от m(-1); 
в този случай конфигурацията, по всяка вероятност започва в началото на m(-2) и завъшрва в края на m4, 
а m1 е х-вълна с обозначение “(х:с3)”. Прочетете следващият абзац.                   
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
   if ( equal_time_price_m0_m2 ) {       
      
      double ratio_p = ComparePriceDirection(-1, 1);
      
      if (Large_161(ratio_p)){         
                
         if ( CheckTimeGoToLength(-1, TimeDirection(-1), 2) ) {
            AddDesignation(desig_c3, "[]");
            
            ratio_p = ComparePriceDirection(-2, -1);
            if (Small_100(ratio_p)){
               AddDesignation(desig_xc3, "()");   
            }
         }         
      }        
   }        
   
   return(0);
} 

int Rule_1_a_paragraph_4(){                          
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако дължината и продължителността на m0 и m2 са равни (или се отнасят с коефициент 61.8%) 
и дължината на m(-1) е по-малка от 161,8% от дължината на m1, 
и дължината на m(-1) превишава дължината на m0 и дължината на m3 или m5 е не по малка от 161.8% от m1, 
може да се образува разновидност на Подвижна Корекция, завършваща по-голяма ценова фигура; 
отбележете това на графиката и запишете “:с3”  в края на m1 до обозначението на “:5”. 
Ако m(-2) е по-дълга от m(-1) се върнете към вълна m(-1) и добавете в нейният списък “:sL3”. 
Ако се образува проста разновидност на Подвижна Корекция, то началото ще е в началото на m0, 
и ще завърши в края на m2, а m1 ще се окаже b-вълна в тази корекция. 
Когато Подвижната Корекция е Сложна Двойна Тройка, m(-2) трябва да бъде по кратка от m(-1), 
и дължината на m3 не трябва да превишава 161.8% дължината на m1; в този случай конфигурацията, 
по всяка вероятност, започва в началото на m(-2) и завъшрва в края на m4, 
а m1 е х-вълна (поставете х пред обзначение “:с3”).
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/      
     
   if ( equal_time_price_m0_m2 ) {       
                      
      double ratio_p = ComparePriceDirection(-1, 1);
      
      if (Small_161(ratio_p)){         
           
         ratio_p = ComparePriceDirection(-1, 0);      
         if (Large_100(ratio_p)){  
                            
            ratio_p = ComparePriceDirection(3, 1);                      
            double ratio_p_1 = ComparePriceDirection(5, 1);
         
            if ( Large_161(ratio_p) || Large_161(ratio_p_1) ) {
               AddDesignation(desig_c3, "");
               
               ratio_p = ComparePriceDirection(-2, -1);
               if (Large_100(ratio_p)){
                  AddDesignation(desig_sL3, "-1");   
               }
               
               ratio_p = ComparePriceDirection(-2, -1);
               if (Small_100(ratio_p)){
               
                  ratio_p = ComparePriceDirection(3, 1);
                  if (Small_161(ratio_p)){
                     AddDesignation(desig_xc3, "");
                  }   
               }                                             
            }
         }         
      }        
   }        
   
   return(0);
} 

int Rule_1_a_paragraph_5(){                             
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако дължината и продулжителността на m0 и m2 са равни (или се отнасят с коефициент 61.8%) 
и дължината на m3 е по-малка от 161.8% от дължината на m1 и началното ниво на m3 (плюс една единица време) 
се достига за период не по-голям от продължителността на m3, 
то m1 може да бъде част от Сложна Корекция, включваща в себе си х-вълна. 
Х-вълна може да се разположи в края на m0 или да е скрита в центъра на вълна m1 (невидима или пропаднала). 
Концепцията за “пропаднала вълна” се обсъжда в Глава 12, стр. 12-34. 
Когато отчитаме тези две възможности, отбелжете “х:с3?” в края на m0, 
отбележете в центъра на m1 кръг и поставете до него “х:с3?” и в ляво напишете ”:s5“. 
Ако m(-2) е по-дълга от m(-1), х-вълна не може да бъде в края на m0, 
ето защо трябва да отхвърлите тази възможност. Ако дължината на m3 е по-малка от 61.8% от дължината на m1, 
най - вероятно х-вълната е скрита в средата на m1.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/       
   if ( equal_time_price_m0_m2 ) {       
                            
      double ratio_p = ComparePriceDirection(3, 1);
      
      double time_m3_break = TimeBreakDirection(3);
      double time_m3 = TimeDirection(3);
      
      
      if (Small_161(ratio_p) && (time_m3_break <= (time_m3+1) && m4_length > m3_length) ){                          
         
         ratio_p = ComparePriceDirection(-2, -1);
         if (Small_100(ratio_p)){
            AddDesignation(desig_xc3_, "0");   
         }
         
         AddDesignation(desig_fall, "1");
         
      }        
   }        
   
   return(0);
} 

int Rule_1_a_paragraph_6(){                                    
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако дължината и/или продължителността на m0 и m2 съществено се отличава една от друга, 
и ценовите проекции на двете вълни не се застъпват, и най късата от  m(-1), m1 и m3 не е m1, 
тогава m1 може да бъде част от по-голяма ценова фигура Трендови Импулс. 
В такав случай поставете по от рано структорно обозначение “:5”
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/     
   if ( !equal_time_price_m0_m2  ) {                       
      
      if (!OverlapDirection(0, 2)){
         if (NoSmallerDirection(1, -1, 3)){
            AddDesignation(desig_5, "");   
         }
      }
   }           
   return(0);
} 