
int Rule_1_b(){
////////////////////////////////////////////////////////////////////////////////////////////////////////////
//Условие “b”: дължината на m0 не по-малка от 61.8%, но по-малка от 100% от дължината m1
////////////////////////////////////////////////////////////////////////////////////////////////////////////
   InfoParagraph = "1-b:1";
   Rule_1_b_paragraph_1();
   
   InfoParagraph = "1-b:2";
   Rule_1_b_paragraph_2();
   
   InfoParagraph = "1-b:3";
   Rule_1_b_paragraph_3();
   
   InfoParagraph = "1-b:4";
   Rule_1_b_paragraph_4();
   
   InfoParagraph = "1-b:5";
   Rule_1_b_paragraph_5();
}

int Rule_1_b_paragraph_1(){   
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Поставете обозначение “:5” в края на m1. 
Ако дължината на m(-1) лежи в границите 100-161.8% (включително) от дължината на m0 и m4 не пресича на края на m0, 
следователно в m1 може да завършва Плоска фигура в границите на Сложна конфигурация, където m2 е х-вълна; 
напишете “:s5” в края на m1 и “x:c3?” в края на m2.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/   
   AddDesignation(desig_5, ""); 
   
   double ratio_p = ComparePriceDirection(-1, 0); 
   if (Large_100(ratio_p) && Small_161(ratio_p)){

      if (CheckDirection(0) == -1){
         if (DirectionMinLevel(4) > DirectionEndPrice(0)){
            AddDesignation(desig_s5, "");
            AddDesignation(desig_xc3_, "2");
         } 
      }else{
         if (DirectionMaxLevel(4) < DirectionEndPrice(0)){
            AddDesignation(desig_s5, "");
            AddDesignation(desig_xc3_, "2");
         }        
      } 

   }
   return(0);
} 

int Rule_1_b_paragraph_2(){ 
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако m0 се състои повече от три моновълни и m1 достига началото на m0  за време, 
не превишаващо времемто за формиране на m0, следователно в m0 може да завършва важна ценова фигура на Елият.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/
   int time_m0_break = TimeBreakDirection(0);
   int time_m0 = TimeDirection(0);
   
   int count_wave = WavesCountInDirection(0);
   if (count_wave > 3 && time_m0_break <= time_m0 && time_m0_break > 0){
      AddDesignation(desig_note, "0");         
   }
   return(0);
}
 
int Rule_1_b_paragraph_3(){   
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако ценовите проекции на m2 и m0 частично се застъпват и m3 е по-дълга и остра от m1, 
в течение на време не превишаващо продължителността на m1, и m(-1) е по- дълга от m1, 
добавете обозначение ”:sL3” в списъка на m1.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/
   if (OverlapDirection(0, 2)){
      double ratio_p = ComparePriceDirection(3, 1);
      
      if (Large_100(ratio_p) && TimeDirection(3) <= TimeDirection(1)){
         ratio_p = ComparePriceDirection(-1, 1);   
          if (Large_100(ratio_p)){
            AddDesignation(desig_sL3, "");  
          } 
      }
   }
   
   return(0);
}
 
int Rule_1_b_paragraph_4(){ 
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако ценовите проекция на m2 и m0 частично се застъпват и m3 е по-дълга и остра от m1, 
в течение на време не превишаващо продължителността на m1, и m(-1) е по-къса от m1, 
и дължината и/или продължителността на m2 и m0 съществено се отличават, 
и m4 (или група вълни m4-m6) достигат до началното ниво на m(-1) за 50% от времето измерено от началото на m(-1) до края на m3, 
следователно в m3 може да завършва Терминален импулс с удължена 5-а, поставете обозначение ”:с3” в списъка на m1.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/
   if (OverlapDirection(0, 2)){
      double ratio_p = ComparePriceDirection(3, 1);
      
      if (Large_100(ratio_p) && TimeDirection(3) <= TimeDirection(1)+1){
         ratio_p = ComparePriceDirection(-1, 1);   
            
         if (Small_100(ratio_p)){
            double ratio_t = CompareTimeDirectionFull(0, 2);
            ratio_p = ComparePriceDirectionFull(0, 2);
               
            if ( !Equal_100(ratio_t) || !Equal_100(ratio_p) ) {
               
               double time = DirectionSumTimeLength(-1, 3);
               
               if (CheckTimeGoToBeginning(-1, time * 0.6, 3)){
                  AddDesignation(desig_c3, "");    
               }
            } 
                    
         }                 
      }
   }  
   return(0);
} 

int Rule_1_b_paragraph_5(){ 
/*
////////////////////////////////////////////////////////////////////////////////////////////////////////////
Ако m3 е по-кратка от m1 и ценовите проекции на m0 и m2 частично се препокриват и m(-1) е по-дълга от m0 
и най-късата вълна от m(-1), m1 и m3 не се явява m1 и след завършването на m3 пазара се връша в началото на m(-1) 
или го подминава за период не по голям от 50% от времето за образуване от началото на (m-1) до края на m3, 
поставете обозначение ”:с3”в края на m1.
////////////////////////////////////////////////////////////////////////////////////////////////////////////
*/
   double ratio_p = ComparePriceDirection(3, 1);
   
   if (Small_100(ratio_p) && OverlapDirection(0, 2) ){
      
      ratio_p = ComparePriceDirection(-1, 0);    
      if (Large_100(ratio_p)){
         
         if (NoSmallerDirection(1, -1, 3)){
         
            double time = DirectionSumTimeLength(-1, 3);
            
            if (CheckTimeGoToBeginning(-1, time * 0.6, 3)){
               AddDesignation(desig_c3, "");    
            }         
         }
      }
   }
   return(0);
} 